apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --abbrev=0 --tags'
        .execute([], project.rootDir).text.trim().replaceAll("v", "")
def gitTagCount = Integer.parseInt('git rev-list --tags --no-walk --count'
        .execute([], project.rootDir).text.trim())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "org.xdty.template"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode gitTagCount
        versionName gitTag

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        setProperty("archivesBaseName", "$rootProject.name-v$versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-debug-rules.pro', 'proguard-rules.pro'
            testProguardFile('proguard-test-rules.pro')
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:${rootProject.ext.supportVersion}"

    implementation "androidx.cardview:cardview:${rootProject.ext.cardVersion}"
    implementation "androidx.recyclerview:recyclerview:${rootProject.ext.supportVersion}"
    implementation "com.google.android.material:material:${rootProject.ext.materialVersion}"
    implementation "androidx.browser:browser:${rootProject.ext.browserVersion}"

    implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintVersion}"


    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"

    implementation "io.requery:requery:${rootProject.ext.requeryVersion}"
    implementation "io.requery:requery-android:${rootProject.ext.requeryVersion}"
    annotationProcessor "io.requery:requery-processor:${rootProject.ext.requeryVersion}"

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-okhttp:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-urlconnection:1.4.2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'

    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:${rootProject.ext.espressoVersion}"

    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.1.0-alpha4') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation "androidx.annotation:annotation:${rootProject.ext.supportVersion}"

    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    // UiAutomator Testing
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'

    androidTestImplementation 'androidx.test:rules:1.1.2-alpha01'

}

apply from: '../signing.gradle'
apply from: '../manifest.gradle'